apply plugin: 'java'
apply plugin: 'jooq'
apply plugin: 'flyway'

buildscript {
  repositories {
    maven {
      url 'http://katta.github.com/repository'
    }
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    classpath 'com.h2database:h2:1.3.167'
    classpath 'org.katta.gradle.api.plugins:flyway:1.3'
    classpath 'com.github.ben-manes:gradle-jooq-plugin:latest.integration'
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.jooq:jooq:3.0.0-RC1'
  compile 'com.google.guava:guava:14.0'
  compile 'com.google.inject:guice:3.0'

  testCompile 'com.h2database:h2:1.3.167'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'com.googlecode.flyway:flyway-core:2.0.3'
  testCompile dependencies.create('org.testng:testng:6.8') {
    exclude group: 'junit'
  }
}

test {
  useTestNG()
}

flywayMigrate.dependsOn(flywayInit)
generateJooq.dependsOn(flywayMigrate)
compileJava.dependsOn(generateJooq)

flyway {
  locations = 'src/main/resources/db/migration'
  url = "jdbc:h2:${buildDir}/db/example"
  driver = 'org.h2.Driver'
  schemas = "public"
  user 'su'
  password = ''
}

flywayInit {
  outputs.upToDateWhen {
    file("${buildDir}/db").exists()
  }
}

generateJooq {
  outputs.upToDateWhen {
    !flywayMigrate.didWork
  }
}

jooq {  
  jdbc {
    url "jdbc:h2:${buildDir}/db/example"
    driver 'org.h2.Driver'
    user 'su'
  }
  generator {
    database {
      name 'org.jooq.util.h2.H2Database'
      inputSchema 'PUBLIC'
      includes '.*'
    }
    generate { 
      deprecated false
    }
    target {
      packageName 'example.generated'
    }
  }
}
