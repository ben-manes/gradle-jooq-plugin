apply plugin: 'signing'
apply plugin: 'groovy'
apply plugin: 'maven'

group = 'com.github.ben-manes'
version = '0.1'

dependencies {
  groovy localGroovy()
  compile gradleApi()

  def jOOQ = "3.0.0-RC1"
  compile "org.jooq:jooq-codegen:$jOOQ"
  compile "org.jooq:jooq-meta:$jOOQ"

  testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
}

repositories {
  maven {
    url = "https://oss.sonatype.org/content/repositories/snapshots"
  }
  mavenCentral()
}

jar {
  manifest {
      attributes 'Implementation-Title': 'Gradle jOOQ plugin',
                 'Implementation-Version': version,
                 'Built-By': System.getProperty('user.name'),
                 'Built-Date': new Date(),
                 'Built-JDK': System.getProperty('java.version'),
                 'Built-Gradle': gradle.gradleVersion
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'javadoc'
  from groovydoc.destinationDir
}

artifacts {
  archives sourcesJar, groovydocJar
}

ext.pomConfiguration = {
  name 'Gradle jOOQ plugin'
  description 'jOOQ codegen for Gradle.'
  url 'https://github.com/ben-manes/gradle-jooq-plugin'
  inceptionYear '2013'

  scm {
    url 'https://github.com/ben-manes/gradle-jooq-plugin'
    connection 'scm:https://ben-manes@github.com/ben-manes/gradle-jooq-plugin.git'
    developerConnection 'scm:git://github.com/ben-manes/gradle-jooq-plugin.git'
  }

  licenses {
    license {
      name 'The Apache Software License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution 'repo'
    }
  }

  developers {
    developer {
      id 'ben-manes'
      name 'Ben Manes'
      email 'ben.manes@gmail.com'
    }
  }
}

install {
  repositories.mavenInstaller {
    pom.project pomConfiguration
  }
}

signing {
  required { gradle.taskGraph.hasTask(uploadArchives) && !version.endsWith('SNAPSHOT') }
  sign configurations.archives
}

uploadArchives {
  repositories.mavenDeployer {
    beforeDeployment { deployment ->
      signing.signPom(deployment)
    }

    name = 'mavenCentralReleaseDeployer'

    doFirst {
      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
        authentication(
          userName: project.mavenCentralUsername,
          password: project.mavenCentralPassword)
        releases(updatePolicy: 'always')
      }
    }

    pom.project pomConfiguration
  }
}

task wrapper(type: Wrapper, description: 'Generates the gradle wrapper.') {
  gradleVersion = '1.4'
}
